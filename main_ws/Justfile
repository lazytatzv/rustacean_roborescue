set shell := ["bash", "-c"]

export COLCON_ROS_SKIP_INSTALL_CHECK := "1"

# colconのoptionについては、colcon.metaで管理するのはやめた

# configs
distro := "jazzy"
common_args := "--merge-install --symlink-install --cmake-args -DROSIDL_GENERATOR_RUST=ON"

# ignore
# pkgをスペース区切りで並べる
ignore_list := "kiss_icp"

ignore_args := if ignore_list != "" { "--packages-ignore " + ignore_list } else { "" }

filter := "2>&1 | grep -v 'doesn.t contain any .local_setup... files' || true"

default: forge

clean:
  @echo "Clean build..."
  rm -rf build/ install/ log/

build-tools:
  @echo "道具だけビルド"
  colcon build {{common_args}} --packages-up-to rosidl_generator_rs {{filter}}

forge: build-tools
  @echo "Pathを通してから残りをビルド"
  source install/setup.bash && colcon build {{common_args}} {{ignore_args}} {{filter}}
  @echo "ビルド完了!"

reforge: clean forge

ready:
  @echo "RUN below command"
  @echo "bass . install/setup.bash"
